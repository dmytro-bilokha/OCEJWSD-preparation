//noinspection GroovyAssignabilityCheck

apply plugin: 'java'
apply plugin: 'war'

war {
    version = '0.1'
    baseName = 'TomcatJerseyRest##'
    classifier = 'SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
def autodeployDir = '/usr/local/apache-tomcat-8.0/webapps/'

def jerseyVersion='2.25.1'

repositories {
    mavenCentral()
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:' + jerseyVersion
    compile 'org.glassfish.jersey.media:jersey-media-moxy:' + jerseyVersion
    compile 'org.slf4j:slf4j-api:1.7.9'
    compile 'org.slf4j:slf4j-jdk14:1.7.9'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    itestCompile 'com.h2database:h2:1.4.192'
}

sourceSets {
    itest {
        java {
            compileClasspath += main.output //+ test.output
            runtimeClasspath += main.output // + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task itest(type: Test) {
    description = 'Runs the integration tests'
    group = 'verification'
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task deployLocal(type: Copy) {
    dependsOn war
    from configurations.archives.allArtifacts*.file
    into autodeployDir
}
